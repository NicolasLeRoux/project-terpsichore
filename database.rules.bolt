path /events {
	read() { true }
	write() { false }
	index() { ["startTime", "organizerId"] }
}

path /events/{eventId} is Event {
	read() { true }
	write() { true }
}

path /unchecked-events/{eventId} is Event {
	read() { true }
	write() { true }
}

path /organizers {
	read() { true }
	write() { false }
	index() { "city" }
}

path /organizers/{organizerId} is Organizer {
	read() { true }
	write() { true }
}

type Event {
	organizerId: String32 | Null,
	organizerName: String32 | Null,
	title: String64,
	startTime: StringDate,
	endTime: StringDate,
	location: Location,
	link: String256 | Null,
	description: String4096,
	facebookId: String32 | Null
}

type Organizer {
	name: String32,
	city: String32,
	facebookId: String32 | Null,
	website: String64 | Null,
	facebook: String64 | Null
}

type Location {
	name: String64,
	city: String32,
	country: String32,
	latitude: Number,
	longitude: Number,
	street: String64 | Null,
	zip: StringZip
}

type String32 extends String {
	validate() { this.length > 0 && this.length <= 32 }
}

type String64 extends String {
	validate() { this.length > 0 && this.length <= 64 }
}

type String256 extends String {
	validate() { this.length > 0 && this.length <= 256 }
}

type String4096 extends String {
	validate() { this.length > 0 && this.length <= 4096 }
}

type StringDate extends String {
	validate() { this.length == 25 }
}

type StringZip extends String {
	validate() { this.length == 5 }
}
